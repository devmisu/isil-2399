plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'kotlin-parcelize'
    id 'androidx.navigation.safeargs.kotlin'
    id 'dagger.hilt.android.plugin'
}

def APP_NAME = "APP_NAME"
def APP_MARKET = "APP_MARKET"
def NAME_BUILD_TYPE = { k -> "\"SoleraJobs ${k}\"" }

android {
    compileSdk appConfig.compileSdkVersion
    defaultConfig {
        minSdk appConfig.minSdkVersion
        targetSdk appConfig.targetSdkVersion
        versionCode appConfig.versionCode
        versionName appConfig.versionName
        applicationId "pe.solera.solerajobs"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
        multiDexEnabled true

        // Only ARM Architecture, avoid run on Emulator ('x86', 'x86_64') 'armeabi-v7a'
        //ndk.abiFilters 'arm64-v8a'
    }

    buildTypes {
        release {
            buildConfigField "String", APP_NAME, NAME_BUILD_TYPE("")
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            minifyEnabled true
            debuggable false
        }
        debug {
            buildConfigField "String", APP_NAME, NAME_BUILD_TYPE("Debug")
            minifyEnabled false
            debuggable true
            applicationIdSuffix ".debug"
        }
    }

    flavorDimensions 'environment'
    productFlavors {
        huawei {
            dimension 'environment'
            buildConfigField "String", APP_MARKET, "\"HMS\""
        }
        google {
            dimension 'environment'
            buildConfigField "String", APP_MARKET, "\"GSM\""
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    kotlinOptions {
        jvmTarget = '11'
    }
    buildFeatures {
        dataBinding true
        viewBinding true
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    implementation "androidx.navigation:navigation-fragment-ktx:$navigation_version"
    implementation "androidx.navigation:navigation-ui-ktx:$navigation_version"

    implementation "com.google.dagger:hilt-android:$hilt_version"
    kapt "com.google.dagger:hilt-android-compiler:$hilt_version"

    implementation "androidx.core:core-ktx:$ktx_core_version"
    implementation "androidx.appcompat:appcompat:$appcompat_version"
    implementation "com.google.android.material:material:$material_version"
    implementation "androidx.constraintlayout:constraintlayout:$constraint_version"
    implementation "androidx.fragment:fragment-ktx:$fragment_ktx_version"

    implementation "androidx.lifecycle:lifecycle-extensions:$lifecycle_ext_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$livedata_version"
    //implementation "androidx.lifecycle:lifecycle-livedata-ktx:$livedata_version"
    implementation "androidx.hilt:hilt-lifecycle-viewmodel:$hilt_lifecycle_viewmodel_version"
    kapt "androidx.hilt:hilt-compiler:$hilt_lifecycle_viewmodel_version"

    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines_version"

    testImplementation "junit:junit:$junit_version"
    androidTestImplementation "androidx.test.ext:junit:$junit_ext_version"
    androidTestImplementation "androidx.test.espresso:espresso-core:$espresso_version"

    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"
    implementation "com.squareup.okhttp3:logging-interceptor:$okhttp3_version"
    implementation "com.squareup.okhttp3:okhttp:$okhttp3_version"
    implementation "com.pddstudio:encrypted-preferences:$encrypted_prefs_version"

    implementation project(path: ':repository')
    implementation project(path: ':entity')
    implementation project(path: ':core')
}